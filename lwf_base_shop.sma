/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <api_oldmenu>
#include <jctf>

#define MAX_ITEM 7

enum _:ITEMS
{
	ITEM_NAME[32],
	ITEM_COST,
	ITEM_FORWARD[32]
}

new g_szItem[MAX_ITEM][ITEMS], g_TotalItems

public plugin_init()
{
	register_plugin("CSGO-System: Shop Natives", "1.1", "Sugisaki")
	
	oldmenu_register()
	
	register_dictionary("lwf_rank.txt")
	register_dictionary("lwf_jctf.txt")
}

public plugin_natives()
{
	register_native("shop_add_item", "_native_add_item")
	register_native("display_shop", "pfn_shop_adrenaline", 1)
}

public _native_add_item(pid, par)
{
	if(g_TotalItems >= MAX_ITEM)
	{
		log_amx("Maximo de items (%i) alcanzado, Modifica el plugin de la tienda.",  MAX_ITEM)
		return PLUGIN_HANDLED
	}
	
	get_string(1, g_szItem[g_TotalItems][ITEM_NAME], 31)
	get_string(3, g_szItem[g_TotalItems][ITEM_FORWARD], 31)
	
	g_szItem[g_TotalItems][ITEM_COST] = get_param(2)
	g_TotalItems++
	
	return PLUGIN_CONTINUE
}

public pfn_shop_adrenaline(id)
{
	oldmenu_create("buymenu_handled", "\rCSGO-System \w%L", id, "MENU_SPITEMS_HEADER")
	
	for(new i = 0; i < g_TotalItems; i++)
	{
		oldmenu_additem((i + 1), i, "\r%d. \w%L: \y%d/%d ADR", (i + 1), id, g_szItem[i][ITEM_NAME], get_user_adrenaline(id), g_szItem[i][ITEM_COST])
	}
	oldmenu_additem(-1, 0, "^n\y%L", id, "MENU_SPITEMS_MSG")
	oldmenu_additem(0, 0, "^n\r0. \w%L", id, "MENU_BACK")
	oldmenu_display(id, 1, -1)
	
	return PLUGIN_HANDLED
}

public buymenu_handled(id, itemnum, value, page)
{
	if(itemnum == 0)
	{
		display_main_menu(id)
	}
	else
	{
		new cost = get_user_adrenaline(id)
		if(g_szItem[value][ITEM_COST] > cost)
		{
			client_print(id, print_chat, "No tienes suficiente adrenalina para comprar este item.")
			return PLUGIN_HANDLED
		}
		
		new ret, g_forward
		g_forward = CreateMultiForward(g_szItem[value][ITEM_FORWARD], ET_STOP, FP_CELL, FP_STRING)
		
		ExecuteForward(g_forward, ret, id, g_szItem[value][ITEM_NAME])
		DestroyForward(g_forward)
		
		if(ret == PLUGIN_HANDLED)
		{
			return PLUGIN_HANDLED
		}
		set_user_adrenaline(id, cost - g_szItem[value][ITEM_COST])
	}
	return PLUGIN_CONTINUE
}
